apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'application'

version = '1.0'
mainClassName = "org.jajuk.Main"
applicationDefaultJvmArgs = ["-XX:MinHeapFreeRatio=5","-XX:MaxHeapFreeRatio=80","-Xms128M","-Xmx612M","-XX:+UseCompressedOops","-XX:+HeapDumpOnOutOfMemoryError","-XX:HeapDumpPath=/tmp",
	"-Djava.library.path=/usr/lib/jni:.:./bin:./lib/native/:./lib/windows/x86:./lib/lib64"]
// org.jajuk.Main -test

sourceCompatibility = 1.8

sourceSets.main.java.srcDirs = ['src/main/java']
sourceSets.main.resources.srcDirs = ['src/main/resources']
sourceSets.test.java.srcDirs = ['src/test/java']

jar {
    manifest {
        attributes 'Implementation-Title': 'Jajuk Media Player', 'Implementation-Version': version
    }
}

repositories {
	mavenCentral()
	//maven { url "http://maven.icm.edu.pl/artifactory/repo/"}
	maven { url "https://repository.jboss.org/nexus/content/repositories/releases/"}
	maven { url "https://nexus.talanlabs.com/content/repositories/releases/"}
	maven { url "https://www.beatunes.com/repo/maven2/"}
}

dependencies {
	compile 'commons-io:commons-io:1.4'
	compile 'org.apache.commons:commons-lang3:3.4'
	compile group: 'com.jgoodies', name: 'animation', version: '1.4.3'
	compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
	compile group: 'com.jhlabs', name: 'filters', version: '2.0.235'
	compile group: 'jfree', name: 'jfreechart', version: '1.0.13'
	compile group: 'org.jcraft', name: 'jorbis', version: '0.0.17'		// 0.13
	compile group: 'de.u-mass', name: 'lastfm-java', version: '0.1.2'
	compile group: 'org.pushingpixels', name: 'trident', version: '1.3'		// 1.5.00-dev
	compile group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.10'
	compile group: 'com.googlecode.soundlibs', name: 'basicplayer', version: '3.0.0.0'
	compile group: 'com.google.guava', name: 'guava', version: '11.0'
	compile group: 'com.jgoodies', name: 'jgoodies-common', version: '1.6.0'
	//compile group: 'org.tritonus', name: 'tritonus_share', version: '0.3.6'
	compile group: 'org.abstractj.libmatthew', name: 'hexdump', version: '0.2.1'
	//compile group: 'com.melloware', name: 'jintellitype', version: '1.3.9'
	compile group: 'org.mobicents.external.jspeex', name: 'jspeex', version: '0.9.7'	// 0.9.3
	compile group: 'log4j', name: 'log4j', version: '1.2.6'
	compile group: 'net.sf.qdwizard', name: 'qdwizard', version: '5.0.1'
	compile group: 'commons-logging', name: 'commons-logging', version: '1.0'
	//compile group: 'org.jaudiotagger', name: 'jaudiotagger', version: '2.0.1'
	compile files('lib/jaudiotagger-2.2.6-20170406.jar')
	compile group: 'javazoom', name: 'jlayer', version: '1.0.1'
	compile group: 'com.github.insubstantial', name: 'laf-plugin', version: '7.3'
	compile group: 'com.miglayout', name: 'miglayout', version: '3.7'
	//compile group: 'com.github.insubstantial', name: 'substance', version: '7.3'		// 8.0.00-dev
	compile files('lib/substance-8.0.00-dev.jar')
	compile group: 'com.vlsolutions', name: 'vldocking', version: '3.0.0'		// 2.1.5
	compile group: 'org.swinglabs', name: 'swingx', version: '1.6'
	compile group: 'javazoom.vorbisspi', name: 'vorbisspi', version: '1.0.3'	// 1.0.1
	compile group: 'com.github.hypfvieh', name: 'dbus-java', version: '2.7.1'	// 2.6
	compile files('lib/entagged-freedb-0.04.jar')
	compile files('lib/cobra_jajuk-0.98.5.jar')

	/*
  js-0.98.4.jar       native                                       unix-0.5.jar
commons-codec-1.3.jar   jcommon-1.0.0.jar                jogg-0.0.7.jar             laf-widget-7.3.jar  mp3spi-1.9.5.jar         
* */

	testCompile 'junit:junit:4.12'
	//testCompile 'org.dstadler:commons-test:1.0.0.16'
}

wrapper {
    gradleVersion = '4.10.3'
}

task adjustWrapperPropertiesFile {
    doLast {
        ant.replaceregexp(match: '^#.*', replace: '', flags: 'g', byline: true) {
            fileset(dir: project.projectDir, includes: 'gradle/wrapper/gradle-wrapper.properties')
        }
        new File(project.projectDir, 'gradle/wrapper/gradle-wrapper.properties').with {
            it.text = it.readLines().findAll { it }.sort().join('\n')
        }
        ant.fixcrlf(file: 'gradle/wrapper/gradle-wrapper.properties', eol: 'lf')
    }
}
wrapper.finalizedBy adjustWrapperPropertiesFile

test {
	maxHeapSize = "12m"
	systemProperties = System.properties

	// enable to show standard out and standard error of the test JVM(s) on the console
	// testLogging.showStandardStreams = true

	// http://forums.gradle.org/gradle/topics/jacoco_related_failure_in_multiproject_build
	systemProperties['user.dir'] = workingDir

    // avoid failures because Gradle provides it's own version of JNA in the system properties
    systemProperties['jna.boot.library.path'] = null
}

jacoco {
	toolVersion = '0.8.4'
}
